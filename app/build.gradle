apply plugin: 'com.component.build'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.dongdongkeji.wangwangsocial"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [host: "app"]
            }
        }

        multiDexEnabled true

        //三方平台配置
        manifestPlaceholders = [qqappid: "1106099147"]
    }

    signingConfigs {

        debug {
            keyAlias 'dongdong'
            keyPassword '123456'
            storeFile file('../dongdongkey.jks')
            storePassword '123456'
        }

        release {
            keyAlias 'dongdong'
            keyPassword '123456'
            storeFile file('../dongdongkey.jks')
            storePassword '123456'
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            buildConfigField "String", "APP_DOMAIN", "\"http://192.168.1.150:8080/\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a"
            }
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            buildConfigField "String", "APP_DOMAIN", "\"http://wangsocial.com:80/\""

            minifyEnabled false     //开启代码混淆
            shrinkResources false    //移除无用的资源文件（AS 3.0.1 后必须开启minifyEnabled后才能开启该标志）
            zipAlignEnabled true    //是否zip对齐优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a"
            }
        }
    }
    //打包apk自动命名，规则：项目名_渠道名_版本号_编译类型.apk
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "wangwang_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${variant.buildType.name}.apk"
        }
    }

    //多渠道打包
    flavorDimensions rootProject.ext.android["versionCode"].toString()
    productFlavors {
        yingyongbao {}
        _360 {}
        oppo {}
        vivo {}
        anzhi {}
        baidu {}
        huawei {}
        lianxiang {}
        wandoujia {}
        xiaomi {}
        sanxing {}
        chuizi {}
        other {}

        productFlavors.all {
            flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //test
    testImplementation 'junit:junit:4.12'
    debugImplementation rootProject.ext.dependencies["canary-debug"]
    releaseImplementation rootProject.ext.dependencies["canary-release"]
    testImplementation rootProject.ext.dependencies["canary-release"]
    //noinspection GradleCompatible
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    //tools
    annotationProcessor rootProject.ext.dependencies["dagger2-compiler"]
    compileOnly rootProject.ext.dependencies['lombok']
    compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    annotationProcessor rootProject.ext.dependencies["lombok-compiler"]
    //View
    annotationProcessor(rootProject.ext.dependencies["butterknife-compiler"]) {
        exclude module: 'support-annotations'
    }
    implementation rootProject.ext.dependencies["constraint-layout"]
    implementation project(':component-service')
    annotationProcessor project(':router-anno-compiler')
    //代替tablayout
    implementation 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
}

combuild {
    applicationName = 'com.wang.social.app.App'
    isRegisterCompoAuto = true
}